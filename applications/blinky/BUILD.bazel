# Copyright 2024 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

load("@bazel_skylib//rules:native_binary.bzl", "native_binary")
load("@hedron_compile_commands//:refresh_compile_commands.bzl", "refresh_compile_commands")
load("@pigweed//targets/host_device_simulator:transition.bzl", "host_device_simulator_binary")
load("@pigweed//targets/rp2040:flash.bzl", "flash_rp2040")
load("//targets:transition.bzl", "rp2040_binary")
load(
    "@pigweed//pw_protobuf_compiler:pw_proto_library.bzl",
    "nanopb_proto_library",
    "nanopb_rpc_proto_library",
)
load("@rules_python//python:proto.bzl", "py_proto_library")
load("@rules_python//python:defs.bzl", "py_binary")

package(default_visibility = ["//visibility:public"])

cc_binary(
    name = "blinky",
    srcs = ["main.cc"],
    deps = [
        ":nanopb_rpc",
        "//libraries/pw_board_led",
        "@pigweed//pw_chrono:system_clock",
        "@pigweed//pw_chrono:system_timer",
        "@pigweed//pw_log",
        "@pigweed//pw_sync:interrupt_spin_lock",
        "@pigweed//pw_system",
    ],
)

# Create an rp2040 flashable ELF
rp2040_binary(
    name = "rp2040_blinky.elf",
    binary = ":blinky",
)

# Create a host binary using the Pigweed upstream pw_system host_device_simulator.
host_device_simulator_binary(
    name = "simulator_blinky",
    binary = ":blinky",
)

# Connect to a running blinky host_device_simulator_binary with:
#   bazel run //applications/blinky:simulator_console
native_binary(
    name = "simulator_console",
    src = "//tools:console",
    # This arg lets us skip manual port selection.
    args = [
        "--socket",
        "default",
    ],
)

native_binary(
    name = "rp2040_console",
    src = "//tools:console",
    args = [
        # TODO(343949763): Remove device path specification if possible.
        "-d",
        "/dev/ttyACM0",
        "-b",
        "115200",
        "--token-databases",
        "$(rootpath //applications/blinky:rp2040_blinky.elf)",
    ],
    data = [":rp2040_blinky.elf"],
)

flash_rp2040(
    name = "flash",
    rp2040_binary = "rp2040_blinky.elf",
)

proto_library(
    name = "proto",
    srcs = ["blinky.proto"],
    import_prefix = "blinky_pb",
    strip_import_prefix = "/applications/blinky",
    deps = [
        "@pigweed//pw_protobuf:common_proto",
    ],
)

nanopb_proto_library(
    name = "nanopb",
    deps = [":proto"],
)

nanopb_rpc_proto_library(
    name = "nanopb_rpc",
    nanopb_proto_library_deps = [":nanopb"],
    deps = [":proto"],
)

py_proto_library(
    name = "py_pb2",
    deps = [":proto"],
)

refresh_compile_commands(
    name = "refresh_compile_commands",
    out_dir = ".compile_commands",
    target_file_names = {
        "//applications/blinky:simulator_blinky": "host_simulator",
        "//applications/blinky:rp2040_blinky.elf": "rp2040",
    },
    targets = {
        "//applications/blinky:simulator_blinky": "",
        "//applications/blinky:rp2040_blinky.elf": "",
    },
)
